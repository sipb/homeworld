load(":upload.bzl", "upload")
load("//bazel:version.bzl", "generate_version_cache")
load(":container_push.bzl", "oci_pusher")
load("//python:resources.bzl", "py_resources")

ocis = {
    "ceph": "//ceph:oci",
    "dns-monitor": "//dns-monitor:oci",
    "dnsmasq-nanny": "//dnsmasq:dnsmasq-nanny",
    "flannel": "//flannel:oci",
    "flannel-monitor": "//flannel-monitor:oci",
    "kube-dns-main": "//kube-dns:kube-dns-main",
    "kube-dns-sidecar": "//kube-dns:kube-dns-sidecar",
    "pause": "//cri-o/pause:oci",
    "pullcheck": "//pull-monitor/pullcheck:oci",
    "user-grant": "//user-grant:oci",
}

basic_debs = [
    "//apt-setup:package",
    "//auth-monitor:package",
    "//autostart:package",
    "//cni-plugins:package",
    "//cri-o:package",
    "//cri-tools:package",
    "//docker-registry:package",
    "//etcd:package",
    "//etcd-metrics-exporter:package",
    "//keysystem:package",
    "//knc:package",
    "//kube-state-metrics:package",
    "//kubernetes:hyperkube",
    "//kubernetes:kubernetes",
    "//oci-sig-policy:package",
    "//oci-tools:package",
    "//prometheus:package",
    "//prometheus-node-exporter:package",
    "//pull-monitor/pull-monitor:package",
    "//runc:package",
    "//services:package",
    "//setup-queue:package",
    "//spire:package",
    "//spire/debian-iso:package",
]

debs = basic_debs + oci_pusher(
    name = "oci-pack",
    images = ocis,
    packagebase = "homeworld-oci-pack",
)

upload(
    name = "upload",
    debs = debs,
    new_version_cache = ":new-version-cache",
    visibility = ["//visibility:public"],
)

generate_version_cache(
    name = "new-version-cache",
    debs = debs,
)

sh_binary(
    name = "parse-branch",
    srcs = [":src/wrapper.sh"],
)

exports_files([
    "BRANCH_NAME",
    "branches.yaml",
    "version-cache",
])

genrule(
    name = "apt-branch-rule",
    srcs = [
        "BRANCH_NAME",
        "branches.yaml",
    ] + ["//upload:" + x for x in glob(["src/*.py"])],
    outs = [
        "DOWNLOAD_URL",
        "DOWNLOAD_URL_HOST",
        "KEYID",
        "SEQID",
    ],
    cmd = "$(location :parse-branch) upload/src/parse.py $(location BRANCH_NAME) $(location branches.yaml) $(location DOWNLOAD_URL) $(location DOWNLOAD_URL_HOST) $(location KEYID) $(location SEQID)",
    tools = [":parse-branch"],
    visibility = ["//visibility:public"],
)

genrule(
    name = "keyexport",
    srcs = [
        "KEYID",
        "SEQID",
    ],
    outs = ["keyring.gpg"],
    cmd = "gpg --export -- \"$$(cat '$(location :KEYID)')\" >'$@'",
    visibility = ["//visibility:public"],
)

genrule(
    name = "keyarmor",
    srcs = [
        "KEYID",
        "SEQID",
    ],
    outs = ["keyring.asc"],
    cmd = "gpg --export --armor -- \"$$(cat '$(location :KEYID)')\" >'$@'",
    visibility = ["//visibility:public"],
)

py_resources(
    name = "uploadlib",
    data = [
        ":BRANCH_NAME",
        ":DOWNLOAD_URL",
        ":keyring.gpg",
    ],
    visibility = ["//visibility:public"],
)
